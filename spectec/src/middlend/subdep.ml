open Util
open Source
open Il.Ast
open Il
open Il.Walk

(* Errors *)

let error at msg = Error.error at "sub expression expansion" msg

(* Environment *)

(* Global IL env *)
let env_ref = ref Il.Env.empty

let empty_tuple_exp at = TupE [] $$ at % (TupT [] $ at)

(* Computes the cartesian product of a given list. *)
let product_of_lists (lists : 'a list list) = 
  List.fold_left (fun acc lst ->
    List.concat_map (fun existing -> 
      List.map (fun v -> v :: existing) lst) acc) [[]] lists

let product_of_lists_append (lists : 'a list list) = 
  List.fold_left (fun acc lst ->
    List.concat_map (fun existing -> 
      List.map (fun v -> existing @ [v]) lst) acc) [[]] lists

let get_quant_id q =
  match q.it with
  | ExpP (id, _) | TypP id 
  | DefP (id, _, _) | GramP (id, _, _) -> id.it

let eq_sube (id, t1, t2) (id', t1', t2') =
  Eq.eq_id id id' && Eq.eq_typ t1 t1' && Eq.eq_typ t2 t2'

let collect_sube_exp e = 
  match e.it with
  (* Assumption - nested sub expressions do not exist. Must also be a varE. *)
  | SubE ({it = VarE id; _}, t1, t2) -> ([id, t1, t2], false)
  | _ -> ([], true)

let check_matching c_args match_args = 
  Option.is_some (try 
    Eval.match_list Eval.match_arg !env_ref Subst.empty c_args match_args 
    with Eval.Irred -> None)

let get_case_typ t = 
  match t.it with
  | TupT typs -> typs
  | _ -> ["_" $ t.at, t]

let collect_all_instances case_typ ids at inst =
  match inst.it with
  | InstD (_, _, {it = VariantT typcases; _}) when 
    List.for_all (fun (_, (t, _, _), _) -> t.it = TupT []) typcases  -> 
    List.map (fun (m, _, _) -> ([], CaseE (m, empty_tuple_exp no_region) $$ at % case_typ)) typcases
  | InstD (_, _, {it = VariantT typcases; _}) -> 
    let _, new_cases = 
      List.fold_left (fun (ids', acc) (m, (t, _, _), _) ->
        let typs = get_case_typ t in
        let new_quants, typs' = Utils.improve_ids_quants ids' true t.at typs in
        let exps = List.map (fun (id, t) -> VarE id $$ id.at % t) typs' in 
        let tup_exp = TupE exps $$ at % t in
        let case_exp = CaseE (m, tup_exp) $$ at % case_typ in
        let new_ids = List.map get_quant_id new_quants in 
        (new_ids @ ids', (new_quants, case_exp) :: acc)  
      ) (ids, []) typcases
    in
    new_cases
  | _ -> error at "Expected a variant type"

let rec collect_all_instances_typ ids at typ =
  match typ.it with
  | VarT (var_id, dep_args) -> let (_, insts) = Il.Env.find_typ !env_ref var_id in 
    (match insts with
    | [] -> [] (* Should never happen *)
    | _ -> 
      let inst_opt = List.find_opt (fun inst -> 
        match inst.it with 
        | InstD (_, args, _) -> check_matching dep_args args
      ) insts in
      match inst_opt with
      | None -> error at ("Could not find specific instance for typ: " ^ Il.Print.string_of_typ typ)
      | Some inst -> collect_all_instances typ ids at inst
    )
  | TupT exp_typ_pairs -> 
    let instances_list = List.map (fun (_, t) -> 
      collect_all_instances_typ ids at t
    ) exp_typ_pairs in
    let product = product_of_lists_append instances_list in
    List.map (fun lst -> 
      let quants, exps = List.split lst in 
      List.concat quants, TupE exps $$ at % typ) product
  | _ -> []

let compute_cases quants subs =
  let ids = List.map get_quant_id quants in
  (* Collect all cases for the specific subtype, generating any potential quants in the process *)
  let _, cases = 
    List.fold_left (fun (quants, cases) (id, t1, _) -> 
      let ids' = List.map get_quant_id quants @ ids in
      let instances = collect_all_instances_typ ids' id.at t1 in 
      let new_quants = List.concat_map fst instances in
      let cases'' = List.map (fun case_data -> (id, case_data)) instances in
      (new_quants @ quants, cases'' :: cases)
    ) (quants, []) subs 
  in
  (* Compute cartesian product for all cases and generate a subst *)
  let cases' = product_of_lists cases in
  List.map (List.fold_left (fun (quants, subst) (id, (quants', exp)) -> 
    (quants' @ quants, Il.Subst.add_varid subst id exp)) ([], Il.Subst.empty)
  ) cases'
  
let collect_typ_fam typ =
  match typ.it with
  | VarT (id, _) ->
    let opt = Env.find_opt_typ !env_ref id in 
    begin match opt with 
      | Some (_, insts) when Utils.check_type_family insts ->
        ([typ], false)
      | _ -> ([], true)
    end
  | _ -> ([], true)

let generate_subst_list lhs quants rhs prems =
  let base_sube_collector = base_collector [] (@) in
  let base_tf_collector = base_collector [] (@) in
  let collector = { base_sube_collector with collect_exp = collect_sube_exp } in
  let tf_collector = { base_tf_collector with collect_typ = collect_typ_fam } in 
  let tfs = 
    List.concat_map (collect_param tf_collector) quants @ 
    collect_exp tf_collector rhs @ 
    List.concat_map (collect_prem tf_collector) prems 
  in
  let subs = List.concat_map (collect_arg collector) lhs @ List.concat_map (collect_typ collector) tfs 
    |> Lib.List.nub eq_sube in
  compute_cases quants subs

let t_clause clause =
  match clause.it with 
  | DefD (quants, lhs, rhs, prems) ->
    let subst_list = generate_subst_list lhs quants rhs prems in
    List.map (fun (quants', subst) -> 
      (* Subst all occurrences of the subE id *)
      let new_lhs = Il.Subst.subst_args subst lhs in
      let new_prems = Il.Subst.subst_list Il.Subst.subst_prem subst prems in
      let new_rhs = Il.Subst.subst_exp subst rhs in

      (* Filtering quants - only the subst ids *)
      let quants_filtered = Lib.List.filter_not (fun b -> match b.it with
        | ExpP (id, _) -> Il.Subst.mem_varid subst id
        | _ -> false
      ) (quants' @ quants) in 
      let new_quants, _ = Il.Subst.subst_params subst quants_filtered in
      (* Reduction is done here to remove subtyping expressions *)
      DefD (new_quants, List.map (Il.Eval.reduce_arg !env_ref) new_lhs, new_rhs, new_prems) $ clause.at
    ) subst_list

let t_inst inst =
  match inst.it with 
  | InstD (quants, lhs, deftyp) ->
    let base_sube_collector : (id * typ * typ) list collector = base_collector [] (@) in
    let collector = { base_sube_collector with collect_exp = collect_sube_exp } in
    (* Collect all unique sub expressions for each argument *)
    let subs = List.concat_map (collect_arg collector) lhs |> Lib.List.nub eq_sube in
    let subst_list = compute_cases quants subs in
    List.map (fun (quants', subst) -> 
      (* Subst all occurrences of the subE id *)
      let new_lhs = Il.Subst.subst_args subst lhs in
      let new_rhs = Il.Subst.subst_deftyp subst deftyp in

      (* Filtering quants - only the subst ids *)
      let quants_filtered = Lib.List.filter_not (fun b -> match b.it with
        | ExpP (id, _) -> Il.Subst.mem_varid subst id
        | _ -> false
      ) (quants' @ quants) in 

      let new_quants, _ = Il.Subst.subst_params subst quants_filtered in
      (* Reduction is done here to remove subtyping expressions *)
      InstD (new_quants, List.map (Il.Eval.reduce_arg !env_ref) new_lhs, new_rhs) $ inst.at
    ) subst_list

let t_rule rule = 
  let RuleD (id, quants, m, exp, prems) = rule.it in
  let base_tf_collector = base_collector [] (@) in
  let tf_collector = { base_tf_collector with collect_typ = collect_typ_fam } in 
  let tfs = 
    List.concat_map (collect_param tf_collector) quants @ 
    collect_exp tf_collector exp @ 
    List.concat_map (collect_prem tf_collector) prems 
  in
  let base_sub_collector = base_collector [] (@) in
  let sub_collector = { base_sub_collector with collect_exp = collect_sube_exp } in
  let subs = List.concat_map (collect_typ sub_collector) tfs |> Util.Lib.List.nub eq_sube in
  let subst_list = compute_cases quants subs in 
  List.map (fun (quants', subst) -> 
    (* Subst all occurrences of the subE id *)
    let new_exp = Il.Subst.subst_exp subst exp in
    let new_prems = Il.Subst.subst_list Il.Subst.subst_prem subst prems in

    (* Filtering quants - only the subst ids *)
    let quants_filtered = Lib.List.filter_not (fun b -> match b.it with
      | ExpP (id, _) -> Il.Subst.mem_varid subst id
      | _ -> false
    ) (quants' @ quants) in 

    let new_quants, _ = Il.Subst.subst_params subst quants_filtered in
    (* Reduction is done here to remove subtyping expressions *)
    RuleD (id, new_quants, m, new_exp, new_prems) $ rule.at
  ) subst_list

let rec t_def def = 
  match def.it with
  | RecD defs -> { def with it = RecD (List.map t_def defs) }
  | DecD (id, params, typ, clauses) ->
    { def with it = DecD (id, params, typ, List.concat_map t_clause clauses) }
  | TypD (id, params, insts) ->
    { def with it = TypD (id, params, List.concat_map t_inst insts)}
  | RelD (id, quants, mixop, typ, rules) ->
    { def with it = RelD (id, quants, mixop, typ, List.concat_map t_rule rules) }
  | _ -> def

let transform (defs : script) =
  env_ref := Il.Env.env_of_script defs;
  List.map (t_def) defs |> Sub.transform